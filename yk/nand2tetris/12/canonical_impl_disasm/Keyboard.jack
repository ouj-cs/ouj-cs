class Keyboard {
    function void init() {
        return;
    }

    function char keyPressed() {
        return Memory.peek(24576);
    }

    function char readChar() {
        var char key, c;
        do Output.printChar(0);

        while ((c = 0) | (key > 0)) {
            let key = Keyboard.keyPressed();

            if (key > 0) {
                let c = key;
            }
        }

        do Output.printChar(String.backSpace());
        do Output.printChar(c);
        return c;
    }

    function String readLine(String message) {
        var char c, newLine, backSpace;
        var String s;
        var boolean break;
        let s = String.new(80);
        do Output.printString(message);
        let newLine = String.newLine();
        let backSpace = String.backSpace();

        while (~break) {
            let c = Keyboard.readChar();
            let break = (c = newLine);

            if (~break) {
                if (c = backSpace) {
                    do s.eraseLastChar();
                } else {
                    let s = s.appendChar(c);
                }
            }
        }

        return s;
    }

    function int readInt(String message) {
        var String s;
        var int i;
        let s = Keyboard.readLine(message);
        let i = s.intValue();
        do s.dispose();
        return i;
    }
}
